apply plugin: 'maven-publish'
apply plugin: 'signing'

task sourcesJar(type: Jar) {
    if (project.hasProperty("kotlin")) {
        from android.sourceSets.main.java.getSrcDirs()
    } else if (project.hasProperty("android")) {
        from android.sourceSets.main.java.sourceFiles
    } else {
        println project
        from sourceSets.main.allSource
    }
    classifier = 'sources'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // Applies the component for the release build variant.
                from components.release
                artifact sourcesJar
//                artifact javadocJar

                groupId = project.group
                artifactId = project.name
                version = project.version

                pom {
                    name = project.name
                    description = project.description
                    url = project.ext.url
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'Agora'
                            name = 'Agora'
                            email = 'devrel@agora.io'
                        }
                    }
                    scm {
                        connection = project.ext.url
                        developerConnection = project.ext.url
                        url = project.ext.url
                    }
                }
            }
        }
        repositories {
            maven {
                name "NEXUS_REPO"
                def snapshotUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
                def releaseUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                url = project.version.endsWith('SNAPSHOT') ? snapshotUrl : releaseUrl
                credentials {
                    username = "${NEXUS_USERNAME}"
                    password = "${NEXUS_PASSWORD}"
                }
                println("maven publish to ${url}")
            }
        }
    }
    signing {
        // keyId and password„ÄÅgpgFilePath defined in .gradle/gradle.properties
        sign publishing.publications.release
    }
}